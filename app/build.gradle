apply plugin: 'com.android.application'

android {
    compileSdk 34
    namespace 'ax.stardust.runcalc'

    defaultConfig {
        applicationId "ax.stardust.runnerscalculator"
        minSdkVersion 24
        targetSdk 34
        versionCode 9
        versionName 'v1.8'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    signingConfigs {
        release
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "version_name", variant.versionName
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField 'boolean', 'DEBUG', 'true'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // configure keystore if git action is running the build
    if (System.getenv("CI") == "true") {
        signingConfigs.release.storeFile file(System.getenv("RUNNER_TEMP") + "/keystore/runcalc.jks")
        signingConfigs.release.storePassword System.getenv("SIGNING_STORE_PASSWORD")
        signingConfigs.release.keyAlias System.getenv("SIGNING_KEY_ALIAS")
        signingConfigs.release.keyPassword System.getenv("SIGNING_KEY_PASSWORD")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }

    lint {
        abortOnError true
        ignoreWarnings false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}